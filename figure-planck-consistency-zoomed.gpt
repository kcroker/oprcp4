# Clean slate
load "madaudickinson2014_stellarz.gpt"

# Define the constraint relation
constraint(w0, dBHp, dBHpp, etap) = dBHp/OmegaL*w0 + ( 1/(3*OmegaL)*(dBHpp + dBHp) - etap)

set table "menzoki.tab"
plot "+" using 1:(constraint($1, 5.356e-5, -2.214e-4, 0)):(constraint($1, 6.135e-5, -2.447e-4, 0)) w filledcurves lc rgb "orange" t '\scalebox{0.5}{Gravastars}'
unset table

# Goodbye tikz.  Reasonable transparency.
set terminal cairolatex color standalone size 6cm,6cm 
set output "figure-planck-consistency-zoomed.tex"

set multiplot
set lmargin at screen 0.15
set tmargin at screen 0.9
set bmargin at screen 0.1
set rmargin at screen 0.95

set key at -0.921, -0.5e-5
set style fill transparent solid 0.5

set format y '\scalebox{0.5}{\shortstack{$%.1t$ \\ $10^{%T}$}}'
set format x '\scalebox{0.5}{%.2f}'

# Xrange
xMin = -1.02
xMax = -0.92
set xrange [xMin:xMax]

# The zoomed y-range
yMin = -0.0002
yMax = 1e-5

set yrange [yMin:yMax]
set ytics offset 0,0 rotate by 45
set xtics 0.02 offset 0.3,0.5
set ylabel '\scalebox{0.5}{$w_a$}' offset 3.2,1.2
set xlabel '\scalebox{0.5}{$w_0$}' offset 0,1.2
set border
set key samplen 1.3

# Add labels 
set label 1 '\scalebox{0.5}{Delayed}' at -0.95, -1.48e-4 rotate by 2
set label 2 '\scalebox{0.5}{Rapid}' at -0.948, -1.78e-4 rotate by 2

# Draw the band in a smooth way
eval(system('./skript.py menzoki.tab second polypoints'))
eval(sprintf("set object 1 polygon behind clip fillcolor rgb 'orange' %s", polypoints))

# Turn off the 2sigma contours
plot 'planck_w0wa.dat' using 1:2 every :::0::0 index 2 smooth bezier w filledcurve lc rgb "red" t '\scalebox{0.5}{Planck+WBR}', '' using 1:2 every :::0::0 index 1 smooth bezier w filledcurve lc rgb "blue" t '\scalebox{0.5}{Planck+BSH}', constraint(x, 5.356e-5, -2.214e-4, 0) lc rgb "orange" lw 2 not, constraint(x, 6.135e-5, -2.447e-4, 0) lc rgb "orange" lw 2 not, '+' using (NaN):(NaN):(NaN) w filledcurves t '\scalebox{0.5}{Gravastars}' lc rgb "orange"

unset object 1

unset label 1
unset label 2
unset xlabel
unset ylabel
unset xtics
unset ytics
unset border

# There is also a vertical constraint relation for specific eta
weff_0(eta, dBHp) = -1 + eta - dBHp/(3*OmegaL)

# Put the crosshairs
set parametric

set label 1 '{\color{white}\scalebox{0.5}{$\eta = 3\times 10^{-2}$}}' front at -0.973, -1e-4 rotate by -90
plot [yMin:yMax] weff_0(3e-2, 6.135e-5), t lw 2 lc rgb "orange" not
unset label 1

set linetype 1 lc rgb "black" lw 1 dt "___"
# Horizontal
plot [xMin:xMax] t,0 not 

# Vertical
plot [yMin:yMax] -1,t not 

unset parametric
unset multiplot
