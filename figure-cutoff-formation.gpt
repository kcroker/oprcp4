load "madaudickinson2014_stellarz.gpt"

set logscale x

set yrange [6:22]
set xrange [1e-3:5e-1]

A = 22.7
Xi = 0.1

drhodt_low = 5.4e-3

# Cutoff a for density production of BH that gives correct present-day \Omega_Lambda 
a_cutoff(eta, onset_z, OL)= eta > 2e-1 ? NaN : (OL*sqrt(Omegam)/A/Xi/drhodt_low*(3*eta - 3.0/2) + (1.0/(1+onset_z))**(-3.0/2 + 3*eta))**(1.0/(3*eta - 3.0/2))

# We will need the density as a function of redshift and eta
# (in order to do a heat map)
rho_no(eta, a)=A*Xi*drhodt_low/sqrt(Omegam)/(3*eta - 3.0/2)*( a**(3*(eta - 1.0/2)) - (1.0/21)**(3*(eta-1.0/2)) )

set term cairolatex colour size 6cm,6cm standalone header '\newcommand{\hl}[1]{\setlength{\fboxsep}{0.75pt}\colorbox{white}{#1}}'
set output "figure-cutoff-formation.tex"

set multiplot

# Explicitly set the margins so gnuplot can try to clip without being retarded
set tmargin at screen 0.15; set bmargin at screen 0.95
set lmargin at screen 0.15; set rmargin at screen 0.95

set style fill transparent solid 0.5 border
unset colorbox
set palette model RGB
set palette defined (0.48 'white', 0.5 'blue', 1 'green', 1.5 'red', 1.52 'white')
set cbrange [0.5:1.5] 

unset key

set pm3d
set view map
unset surface
set isosamples 1000,1000
unset xtics
unset ytics
unset ylabel
unset xlabel

splot rho_no(x, 1.0/(1+y))/OmegaL

# Draw exclusion rectangle
set object 6 rectangle behind noclip fs pattern 7 transparent fillcolor rgb 'gray' from first 6e-2,GPVAL_Y_MAX to first 5e-1, GPVAL_Y_MIN
set label 1 front at 3.75e-2,15.55 '\scalebox{0.5}{$\Omega_\Lambda/2$}' rotate by -35
set label 2 front at 1e-2,12.2 '\scalebox{0.5}{$\Omega_\Lambda$}' rotate by -17
set label 3 front at 3e-3,8 '\scalebox{0.5}{$3\Omega_\Lambda/2$}' rotate by -10
set label 4 front at 1.5e-3, 20.6 '\scalebox{0.5}{Stellar onset $z_\mathrm{early}$}' 
set label 5 front at 1.5e-3, 9.5 '\scalebox{0.5}{Reionization $z_\mathrm{re}$}'

unset border
unset pm3d
unset map
unset contour

set format y '\scalebox{0.5}{%.0f}' 
set ytics 6,2,24 offset 0.5,0
set mytics
set ylabel '\scalebox{0.5}{Formation cutoff redshift $z_f$}' offset 5.5,0

set format x '\scalebox{0.5}{$10^{%T}$}'
set xtics offset 0,0.4
set xlabel '\scalebox{0.5}{Gravastar $\eta$}' offset 0,1

plot '+' using 1:(1/a_cutoff($1, 20, 1.49*OmegaL) - 1) w l lw 3 lc rgb "red" not, '' using 1:(1/a_cutoff($1, 20, OmegaL) - 1) w l lw 3 lc rgb "green" not, '' using 1:(1/a_cutoff($1, 20, 0.515*OmegaL) - 1) w l lw 4 lc rgb "blue" not, 20 dt '-' lc rgb 'black', 8.9 dt '-' lc rgb 'black'

unset multiplot
